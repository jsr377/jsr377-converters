/*
 * Copyright 2015-2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'idea'
    id 'org.kordamp.gradle.build-scan' version '0.17.0'
    id 'org.kordamp.gradle.project'    version '0.17.0'
    id 'org.jonnyzzz.java9c'           version '0.2.3'
    id 'com.github.kt3k.coveralls'     version '2.8.2'
}

buildScan {
    termsOfServiceAgree = 'yes'
}

if (!project.hasProperty('bintrayUsername'))  ext.bintrayUsername  = '**undefined**'
if (!project.hasProperty('bintrayApiKey'))    ext.bintrayApiKey    = '**undefined**'
if (!project.hasProperty('sonatypeUsername')) ext.sonatypeUsername = '**undefined**'
if (!project.hasProperty('sonatypePassword')) ext.sonatypePassword = '**undefined**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name          = 'jsr377-converters'
        vendor        = 'JSR-377'
        description   = 'JSR 377 - Converters for basic types'
        inceptionYear = '2015'
        tags          = ['desktop', 'java', 'jsr377']

        links {
            website      = 'https://github.com/jsr377/jsr377-converters'
            issueTracker = 'https://github.com/jsr377/jsr377-converters/issues'
            scm          = 'https://github.com/jsr377/jsr377-converters.git'
        }

        people {
            person {
                id    = 'aalmiray'
                name  = 'Andres Almiray'
                roles = ['Spec Lead', 'Developer']
            }
        }

        specification {
            title = 'JSR-377 API'
        }

        implementation { enabled = false }

        credentials {
            sonatype {
                username = project.sonatypeUsername
                password = project.sonatypePassword
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    bintray {
        credentials {
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
        userOrg = 'jsr377'
        name    = 'jsr377-api'
    }

    stats {
        formats = ['xml', 'txt']
    }

    javadoc {
        autoLinks {
            enabled = false
        }
    }
}

idea {
    project {
        jdkName rootProject.sourceCompatibility
        languageLevel rootProject.sourceCompatibility
    }
}

repositories {
    jcenter()
}

dependencies {
    compile "javax.application:jsr377-api:$jsr377Version"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    testCompile "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junit5Version"
    testCompile "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    testCompile "org.hamcrest:java-hamcrest:$hamcrestVersion"
    testRuntime "org.slf4j:slf4j-simple:$slf4jVersion"
}

test {
    useJUnitPlatform()

    reports {
        html.enabled = true
    }
}

jar {
    manifest {
        attributes(
            'Automatic-Module-Name': 'org.kordamp.jsr377.converters'
        )
    }
}